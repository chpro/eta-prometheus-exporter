plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.6.3"
}

version = "0.1"
group = "chpro.eta.proexp"

repositories {
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/chpro/eta-api-java"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("PACKAGES_READ_TOKEN")
        }
    }
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-http-validation")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("jakarta.annotation:jakarta.annotation-api")
    runtimeOnly("ch.qos.logback:logback-classic")
    implementation("io.micronaut:micronaut-validation")
    implementation("chpro.eta.api:eta-api:0.1")
    implementation("io.prometheus:simpleclient:0.16.0")
    implementation("io.prometheus:simpleclient_servlet_common:0.16.0")

}


application {
    mainClass.set("chpro.eta.proexp.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("chpro.eta.proexp.*")
    }
}

// https://github.com/micronaut-projects/micronaut-gradle-plugin/issues/214
dockerBuild {
  inputDir = file("${buildDir}/docker/main")
  images = ["ghcr.io/chpro/${project.name}:${project.version}"]
}


task fixIssue214(type: Copy){
    from ('Dockerfile')
    into "${buildDir}/docker/main"
}

dockerBuild.dependsOn fixIssue214

dockerBuild.dockerFile.set(file("${buildDir}/docker/main/Dockerfile"))
// ^^^
